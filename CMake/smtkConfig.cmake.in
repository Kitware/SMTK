#------------------------------------------------------------------------------
# Update CMAKE_PREFIX_PATH locally when SMTK_RELOCATABLE_INSTALL=OFF
#------------------------------------------------------------------------------

set(SMTK_PREFIX_PATH "@smtk_prefix_path@")
set(CMAKE_PREFIX_PATH_save "${CMAKE_PREFIX_PATH}")
list(APPEND CMAKE_PREFIX_PATH ${SMTK_PREFIX_PATH})

#Setup the CMake module paths
list(APPEND CMAKE_MODULE_PATH "@SMTK_MODULE_DIR@;@SMTK_VTK_MODULE_DIR@")
set(smtk_module_dir "@SMTK_VTK_MODULE_DIR@/Modules")
if(IS_DIRECTORY "${smtk_module_dir}")
  list(APPEND CMAKE_MODULE_PATH "${smtk_module_dir}")
  file(GLOB config_files RELATIVE "${smtk_module_dir}" "${smtk_module_dir}/*.cmake")
  foreach (_file IN LISTS config_files)
    if (NOT _file MATCHES "[^\\-]+-[a-zA-Z]+\\.cmake")
      string(REGEX REPLACE "\\.cmake$" "" _module "${_file}")
      list(APPEND VTK_MODULES_ENABLED "${_module}")
    endif()
  endforeach()
endif()
unset(smtk_module_dir)

set(smtk_cmake_dir ${CMAKE_CURRENT_LIST_DIR})

set(SMTK_HAS_PYTHON @SMTK_ENABLE_PYTHON_WRAPPING@)
if (SMTK_HAS_PYTHON)
  find_package(pybind11 REQUIRED)
endif ()

#When building documentation internally we re-import ourselves, which
#doesnt work and generates CMake warnings. So we will only look for SMTK
#if the smtkCore target doesn't already exist
if(TARGET smtkCore)
else()

  find_package(Boost 1.64.0
             COMPONENTS @required_boost_components@ REQUIRED)
  find_package(nlohmann_json REQUIRED)
  find_package(MOAB REQUIRED)

  set(SMTK_ENABLE_PARAVIEW_SUPPORT @SMTK_ENABLE_PARAVIEW_SUPPORT@)
  if(SMTK_ENABLE_PARAVIEW_SUPPORT)
    find_package(ParaView)
  endif()

  set(SMTK_ENABLE_VTK_SUPPORT @SMTK_ENABLE_VTK_SUPPORT@)
  if(SMTK_ENABLE_VTK_SUPPORT AND NOT SMTK_ENABLE_PARAVIEW_SUPPORT)
    find_package(VTK REQUIRED)
  endif()

  set(SMTK_ENABLE_REMUS_SUPPORT @SMTK_ENABLE_REMUS_SUPPORT@)
  if(SMTK_ENABLE_REMUS_SUPPORT)
    find_package(Remus REQUIRED)
  endif()

  set(SMTK_ENABLE_PYTHON_WRAPPING @SMTK_ENABLE_PYTHON_WRAPPING@)
  if(SMTK_ENABLE_PYTHON_WRAPPING)
    find_package(pybind11 2.3 REQUIRED)
  endif()

  set(SMTK_PLUGINS @SMTK_PLUGINS@)

  include("@SMTK_CONFIG_DIR@/smtkTargets.cmake")
endif()

#------------------------------------------------------------------------------
# restore CMAKE_PREFIX_PATH
#------------------------------------------------------------------------------

set(CMAKE_PREFIX_PATH "${CMAKE_PREFIX_PATH_save}")
unset(CMAKE_PREFIX_PATH_save)
